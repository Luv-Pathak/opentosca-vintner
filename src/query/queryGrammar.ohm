Query {
  Main = (Expression | MatchExpression) end
  Expression = (FromInstance | FromTemplate) Select
  MatchExpression = (FromInstance | FromTemplate) Match

  FromInstance = fromLiteral ident "/" ident
  FromTemplate = fromLiteral (asterisk | ident)
  Select = selectLiteral (StepCond | Step) ("." (StepCond | Step))*
  Step = ident
  StepCond = (asterisk | ident) "[" Predicate "]"
  Predicate = Predicate logic Predicate -- multi
            | Condition -- single
  Condition = Value comparison Value
  Match = matchLiteral Node (Relationship Node)*
  Node = "(" Step+ ")"
  Relationship = "-[" ident "]->"

  Value = path | literal

  fromLiteral = "FROM"
  matchLiteral = "MATCH"
  selectLiteral = "SELECT"

  asterisk = "*"
  comparison = ("=" | "!=" | ">=" | "<=" | ">" | "<")
  ident = letter (alnum | "_" | "-")*
  logic = "AND" | "OR"
  path = letter (alnum | "_" | "-" | ".")*
  literal = string | float | digit | bool
  bool = "true" | "false" | "TRUE" | "FALSE"
  string
    = "'" (~"'" any)* "'"
    | "\"" (~"\"" any)* "\""
  float   = digit? "." digit+

  comment = "/*" (~"*/" any)* "*/"
  space += comment
}
