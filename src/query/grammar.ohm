Query {
  Main = (Expression | MatchExpression) end
  Expression = FromExpression Select
  MatchExpression = FromExpression Match Select

  FromExpression = fromLiteral ("instance" | "template") ("/" | ".") (asterisk | filePath)

  Select = selectLiteral Path ("," Path)*
  Path = (Group | Policy | StepCond | Step | ".") ("." (StepCond | Step))* ReturnClause?
  Step = shortcut? (asterisk | ident)
  StepCond = shortcut? (asterisk | ident) PredicateExpression
  ReturnClause = "{" KeyValuePair ("," KeyValuePair)* "}"
  KeyValuePair = Variable ":" Variable --complex
               | Variable              --simple
  Group = groupLiteral "(" ident ")"
  Policy = policyLiteral "(" ident ")"

  PredicateExpression = "[" Predicate "]"
  Predicate = Predicate logic Predicate -- multi
            | Condition -- single
  Condition = Value comparison Value -- comparison
            | Value -- existence
  Match = matchLiteral Node (Relationship Node)*
  Node = "(" ident? PredicateExpression? ")"
  Relationship = arrowLeft arrowRight --simple
               | arrowLeft "{" ident? PredicateExpression? Cardinality? "}" arrowRight --cond
  Cardinality = asterisk integer ".." integer --range
              | asterisk ".." integer --max
              | asterisk integer ".." --min
              | asterisk integer --exact
              | asterisk --unlimited
  Variable = literal | ident
  Value = shortcut? (path | literal)

  fromLiteral = "FROM"
  groupLiteral = "GROUP"
  matchLiteral = "MATCH"
  policyLiteral = "POLICY"
  selectLiteral = "SELECT"

  arrowLeft = "<-" | "-"
  arrowRight = "->" | "-"
  asterisk = "*"
  comparison = "=~" | "=" | "!=" | ">=" | "<=" | ">" | "<"
  ident = letter (alnum | "_" | "-")*
  logic = "AND" | "OR"
  path = letter (alnum | "_" | "-" | ".")*
  filePath = (letter | alnum | "_" | "-" | "." | "/")*
  literal = string | float | digit | bool
  bool = "true" | "false" | "TRUE" | "FALSE"
  shortcut = "@" | "#" | ":"
  string
    = "'" (~"'" any)* "'"
    | "\"" (~"\"" any)* "\""
  integer = digit+
  float   = digit? "." digit+
  singleComment = "//" (~"\n" any)*
  multiComment = "/*" (~"*/" any)* "*/"
  space := singleComment | multiComment | ...
}
